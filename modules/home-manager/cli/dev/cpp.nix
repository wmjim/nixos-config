{ lib, config, pkgs, ... }:
let
  cfg = config.mengw.cli.dev.cpp;
  devCfg = config.mengw.cli.dev;
  cliCfg = config.mengw.cli;

  # mcpp：C++23 模块优先的构建工具（上游 mcpp-community/mcpp），仓库自打包在
  # pkgs/mcpp-m（预编译 bundle + MCPP_HOME wrapper，理由见该文件顶部）。
  # 名字不能省成 `pkgs.mcpp`：nixpkgs 的 mcpp 是 Matsui 的 C 预处理器，同名不同物。
  mcpp = pkgs.callPackage ../../../../pkgs/mcpp-m { };
in
{
  options.mengw.cli.dev.cpp.enable = lib.mkOption {
    type = lib.types.bool;
    default = true;
    description = "启用 C/C++ 开发环境";
  };

  config = lib.mkIf (cfg.enable && devCfg.enable && cliCfg.enable) {
    # C/C++ 开发环境
    home.packages = with pkgs; [
      # 编译器工具链（只用 clang，避免与 gcc 冲突）
      clang # c/c++ lsp
      cmake # 跨平台构建系统生成器
      cmake-language-server # cmake lsp
      gnumake
      ninja # 快速构建工具

      # C++ 包管理器和依赖管理
      vcpkg # C++ 包管理器
      xmake # 跨平台构建工具和包管理器
      mcpp # C++23 模块优先的构建/包/工具链管理器（首次运行会联网初始化 ~/.mcpp）
      ccache # 编译缓存，加速重复编译

      # 调试和分析工具
      lldb # LLVM 调试器
      valgrind # 内存泄漏和性能分析工具

      # 代码质量和静态分析
      cppcheck # C++ 静态分析工具
      clang-tools # 包含 clang-format, clang-tidy, clangd

      # 构建依赖管理
      pkg-config # 编译时依赖配置工具
    ] ++ lib.optionals pkgs.stdenv.hostPlatform.isLinux (with pkgs; [
      # Linux 专属性能分析工具
      perf-tools # Linux 性能分析工具
      strace # 系统调用追踪
      ltrace # 库调用追踪
    ]);
  };
}
